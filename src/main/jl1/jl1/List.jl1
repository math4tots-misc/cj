package jl1

import jl1.IO
import jl1.StringBuilder

class List[T] {

    class Iterator[T] {
        val list: List[T]
        var i = 0
        def hasNext(this): bool = i < list.size
        def next(this): T = {
            val t = list[i]
            i++
            t
        }
    }

    var array: T[]
    var size: int

    def empty(): Self = Self.new(T[].new(0), 0)

    def fromArray(array: T[]): Self = Self.new(array, array.size())

    def size(this): int = size
    def __getitem(this, i: int): T = array[i]
    def __setitem(this, i: int, t: T) = (array[i] = t)

    def map(this, f: Fn[T, int]): List[int] {
        val ret = List[int].empty()
        for t in this {
            ret.add(f.call(t))
        }
        ret
    }

    def __eq(this, other: Self): bool {
        if size != other.size {
            false
        } else {
            var eq = true
            for i = 0; eq and i < size; i++ {
                eq = array[i] == other.array[i]
            }
            eq
        }
    }

    def add(this, t: T) {
        if size < array.size() {
            array[size] = t
        } else {
            val newcap = if array.size() { 2 * array.size() } else { 4 }
            array = array.realloc(newcap)
            array[size] = t
        }
        size++
    }

    def __add(this, other: Self): Self {
        val newSize = size + other.size
        val newArray = T[].new(newSize)
        for i = 0; i < size; i++ {
            newArray[i] = array[i]
        }
        for i = 0; i < other.size; i++ {
            newArray[size + i] = other.array[i]
        }
        Self.new(newArray, newSize)
    }

    def iter(this): Iterator[T] = Iterator[T](this)

    def toString(this): String {
        val sb = StringBuilder()
        sb.add("[")
        var i = 0
        while i < size {
            if i > 0 {
                sb.add(", ")
            }
            sb.add("" + array[i])
            i = i + 1
        }
        sb.add("]")
        sb.toString()
    }
}
